
def libBaseDir = "${project(':lib-native').buildDir}/lib/main/release"
def pathSep = System.getProperty('path.separator')
def libraryPath = System.getProperty('java.library.path').split(pathSep)
def mainClassName ='jp.co.atware.ac2021.jni.app.Application'

subprojects {
    apply plugin: 'application'

    application {
        mainClass = mainClassName
    }

    run {
        switch (System.getProperty('os.name')) {
            case 'Linux':
                systemProperty('java.library.path', (libraryPath + ["$libBaseDir/linux"]).join(pathSep))
                break;
            case 'Mac OS X':
                systemProperty('java.library.path', (libraryPath + ["$libBaseDir/macos"]).join(pathSep))
                break;
        }

        args "$performDataSize", "$performDataChunkSize"
    }

    jar {
        manifest {
            attributes(
                'Main-Class': mainClassName,
                'Class-Path': "${project(':lib-jni').name}.jar",
            )
        }
    }

    dependencies {
        implementation project(':lib-jni')
    }
}
